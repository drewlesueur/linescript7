# Variables
name = "Ada"
age = 30
active = TRUE

# Array literal (1-based)
numbers = [1 2 3 4 5]
numbers[3] = 99

# Object literal
user = { name: name age: age active: active }
user.city = "Phoenix"

# Array of objects
people = [
    {
        name: "Ada"
        age: 30
    }
    {
        name: "Alan"
        age: 41
    }
]

# String operations
firstPart = SUBSTR name 1 3
combined2 = firstPart + name
phrase = STRING He said "hi"
poem = STRING
    He said "hi"
    He also said "bye"
    Cool lines
END
trimmed = TRIM "  padded  "
startsOk = STARTS_WITH name "Ad"
endsOk = ENDS_WITH name "da"

# Arithmetic
total = SUM_ARRAY numbers
average = total / LEN numbers

# IF / ELSE IF / ELSE
IF average > 10
    grade = "A"
ELSE IF average > 7
    grade = "B"
ELSE
    grade = "C"
END

# Boolean logic and comparisons
IF active AND age >= 18
    canVote = TRUE
ELSE
    canVote = FALSE
END

IF grade IS "A" OR grade IS "B"
    honors = TRUE
ELSE
    honors = FALSE
END

# NOT and ISNT
IF NOT active
    status = "inactive"
END

IF user.city ISNT "Phoenix"
    status = "remote"
END

# FOR loop (inclusive end value)
randomValues = []
FOR i FROM 1 TO 5
    value = RAND 1 100
    randomValues[i] = value
END

# FOR loop (index and value)
FOR idx val IN numbers
    numbers[idx] = val + 1
END

# FOR loop (value only)
FOR val IN numbers
    total = total + val
END

# FOR loop (object keys and values)
FOR key val IN user
    lastKey = key
END

# Array stack/queue operations
PUSH randomValues 42
lastValue = POP randomValues
UNSHIFT randomValues 7
firstValue = SHIFT randomValues

# FOR with CONTINUE and BREAK
i = 1
FOR i <= LEN randomValues
    IF randomValues[i] < 10
        i = i + 1
        CONTINUE
    END

    IF randomValues[i] > 90
        BREAK
    END

    i = i + 1
END

# SPLIT and JOIN
words = SPLIT "one,two,three" ","
joined = JOIN words "-"

# Right-associative function calls
joined2 = JOIN SPLIT "one two three" " " "-"
joined3 = JOIN (SPLIT "one two three" " ") "-"

# Nested grouping expression
fancy = (SUBSTR joined 1 3) + (SUBSTR joined 5 3)

# Operator precedence (parentheses override)
calc1 = 1 + 2 * 3
calc2 = (1 + 2) * 3

# NULL literal
missing = NULL

# NULL checks and guards
IF missing IS NULL
    missing = "fallback"
END

tenth = numbers[10]
IF tenth IS NULL
    tenth = 0
END

IF user.zip IS NULL
    user.zip = "00000"
END

# Missing key/index yields NULL
missingKey = user.nope
missingIndex = numbers[999]

# LEN works on arrays, strings, and objects
lenNumbers = LEN numbers
lenName = LEN name
lenUser = LEN user

# Function definitions and calls
FORMAT_NAME first last
    RETURN first + " " + last
END

AVERAGE_OF_ARRAY values
    total = SUM_ARRAY values
    RETURN total / LEN values
END

DEFAULT_STATUS
    RETURN "unknown"
END

# Function with implicit return (last expression)
DOUBLE n
    n * 2
END

displayName = FORMAT_NAME "Ada" "Lovelace"
avgScore = AVERAGE_OF_ARRAY [90 80 100]
fullName = FORMAT_NAME name user.city
status = DEFAULT_STATUS
doubled = DOUBLE 21

# Output
PRINT displayName

# Stack application
PRINT UPPER SLICE "Drew" 1 2

# Same as
"Drew"
SLICE 1 2
UPPER
PRINT

# Same as
"Drew"; SLICE 1 2; UPPER; PRINT

# Operator precedence follows conventional rules, functions bind tighest
